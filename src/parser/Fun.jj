/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
  static = false; //Nao criara metodos estaticos
  DEBUG_LOOKAHEAD = true;
}

PARSER_BEGIN(Fun)
package parser;
import java.io.*;
import recovery.*;
import syntacticTree.*;
import semanalysis.*;   // importa as classes para a analise semantica


public class Fun {
  final static String Version ="X++ Compiler - version 1.0 - 2018\n";
  int contParseError = 0;
  boolean debug_recovery;
  
  public static void main(String args []) throws ParseException {
    boolean debug_as = false;
    boolean debug_recovery = false;
    boolean print_tree = false;
    
	String filename ="";
    Fun parser;
    int i;
    boolean ms = false;
    
	System.out.println(Version);
	// le os parametros passados para o compilador
	for (i = 0; i<args.length - 1; i++)
	{
	  if (args[i].equals("-debug_AS")) 
	  	debug_as = true;
	  else
	  if (args[i].equals("-debug_recovery"))
	    debug_recovery = true;
	  else
      if (args[i].equals("-print_tree") )
        print_tree = true;
	  else
	  {
	    System.out.println("Usage is: java Fun [-debug_AS] " + "[-debug_recovery] [-print_tree]] inputfile");
	    System.exit(0);
	  }
	}
	  
	
	if(args[i].equals("-")) {
	  // le a entrada - padrao
	  System.out.println("Reading standart input...");
	  parser = new Fun(System.in);
	}
	else {
	  // le do arquivo
	  filename = args[args.length-1];
	  System.out.println("Reading from file " + filename + "...");
	  try {
	    parser = new Fun(new java.io.FileInputStream(filename));
	  }
	  catch (java.io.FileNotFoundException e) {
	    System.out.println("File " + filename + " not found.");
	    return;
	  }
	}
	

	parser.debug_recovery = debug_recovery;
	if (!debug_as) parser.disable_tracing(); // desab verbose do AS
	try {
		parser.program(); //chama o metodo que faz a analise
	}
	catch (ParseEOFException e) {
	  System.err.println(e.getMessage());
	}
	finally {
	   System.out.println(parser.token_source.foundLexError() + "Lexical errors found");
	   System.out.println(parser.contParseError + " Syntatic errors found");
	 }

	 if ( parser.token_source.foundLexError() // verifica se pode imprimir
         + parser.contParseError == 0 && print_tree) // a arvore sintatica
    {
//        PrintTree prt = new PrintTree();
//        prt.printRoot(root);     // chama metodo para imprimir arvore
    }

	// verifica se houve erro lexico
	if (parser.token_source.foundLexError() != 0) {
	  System.out.println(parser.token_source.foundLexError() + " lexical errors found");
	}
	else {
	  System.out.println("Program successfully analized");
	} 
} // main

static public String im (int x)
{
	int k;
    String s;
    s = tokenImage[x];
    k = s.lastIndexOf("\"");
    try { s = s.substring(1,k); }
    catch (StringIndexOutOfBoundsException e)
    { }
    return s;
}

boolean eof; // variavel que indica se EOF foi alcancado

// o metodo abaixo consome tokens ate alcancar um que pertenca ao conjunto de sincronizacao
void consumeUntil(RecoverySet g, ParseException e, String met) throws ParseEOFException, ParseException
{
  	Token tok;
	if (debug_recovery) {
	  System.out.println();
	  System.out.println("*** " + met + " ***");
	  System.out.println("   Syncronizing Set: " + g);
	}
	if (g==null) throw e; // se o conjunto eh null, propaga a execao
	tok = getToken(1); // pega token corrente
	
	while (!eof) {
	  if (g.contains(tok.kind)) {
	    if (debug_recovery) 
	    	System.out.println("Found syncronizing token: " + im(tok.kind));
	    break;
	  }
	  if(debug_recovery) 
	  	System.out.println("Ignoring token: " + im(tok.kind));
	  getNextToken(); //pega prox token
	  tok = getToken(1);
	  if (tok.kind == EOF && ! g.contains(EOF) ) //fim da entrada
	  	eof = true;
	  }
	System.out.println(e.getMessage());
	contParseError++; // incremento o numero de erros
	if ( eof ) throw new ParseEOFException("EOF found prematurely.");
}
	
} // Fun  

PARSER_END(Fun)

TOKEN_MGR_DECLS :
{
  int countLexError = 0;

  public int foundLexError() {
    return countLexError;
  }
}


SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
| "\f"
}

TOKEN : /* Palavra$ reservadas */
{
 < BREAK: "break">
| < CLASS: "class">
| < CONSTRUCTOR: "constructor">
| < ELSE: "else" >
| < EXTENDS: "extends">
| < FOR: "for" >
| < IF: "if" >
| < INT: "int" >
| < NEW: "new" >
| < PRINT: "print" >
| < READ: "read" >
| < RETURN: "return" >
| < STRING: "string" >
| < SUPER: "super" >
| < FLOAT: "float" >
| < CHAR: "char" >
| < BOOLEAN: "boolean" >

}

TOKEN : /* Operadore$ */
{
  < PLUS : "+" >
| < MINUS : "-" >
| < MULTIPLY : "*" >
| < DIVIDE : "/" >
| < REM: "%" >
| < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NEQ: "!=" >
| < NOT: "not" >
| < AND: "and" >
| < XOR: "xor" >
| < OR: "or" >

}

TOKEN : /* $imbolos especiais */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

TOKEN : /* Contante$ */
{
	< int_constant:( // numeros decimais, octais, hexa ou binarios
		(["0"-"9"] (["0"-"9"])* ) |
		(["0"-"7"] (["0"-"7"])* ["o", "O"]) |
		(["0"-"9"] (["0"-"7","A"-"F", "a"-"f"])* ["h", "H"]) |
		(["0"-"1"] (["0"-"1"])* ["b", "B"])
		) >
|
	< string_constant: //qlqr constante string 
	"\""
    (
      (~[ "\"", "\\", "\n", "\r" ])
    | "\\" [ "n", "t", "r", "\\", "\"" ]
    )*
    "\"" >
|
	< char_constant:
	"\'"
    (
      (~["\\", "\n", "\r", "\'" ])
    | "\\" [ "n", "t", "r", "\\", "\"", "\'" ]
    )
    "\'" >
|
	< float_constant:( // numeros de ponto flutuante
	(["-"])? (["0"-"9"])+ ["."] (["0"-"9"])+
	) >
|	
	< boolean_constant: "true" | "false" > // constante boolean
|
	< null_constant: "null" > // constante nula


}

TOKEN: /* identificadore$ */
{
  < IDENT: < LETTER > (< LETTER >|< DIGIT >)* >
| 
  < #LETTER:["A"-"Z","a"-"z"] >
|
  < #DIGIT:["0"-"9"] >

}


SKIP:
{
  "/*": multilinecomment
}
<multilinecomment> SKIP:
{
 	 "*/": DEFAULT
	|  <~[]>
}


SKIP:
{
  "//": singlelinecommnet
}
<singlelinecommnet> SKIP:
{
  < ["\n","\r"] >: DEFAULT
|  <~[]>
}

SPECIAL_TOKEN:
{
< INVALID_LEXICAL:
(~ ["a"-"z", "A"-"Z",
	"0"-"9",
	"\"",
	"\'",
	"(",
	")",
	"[",
	"]",
	"{",
	"}",
	";",
	",",
	".",
	"=",
	">",
	"<",
	"!",
	"+",
	"-",
	"*",
	"/",
	"%",
	" ",
	"\t",
	"\n",
	"\r",
	"\f"
])+>
	{
	  System.err.println("Line " + input_stream.getEndLine() + " - Invalid string found: " + image);
	  countLexError++;
	}
|
	< INVALID_CHAR_CONST:
	"\'"
    (
      (~[ "\'", "\\", "\n", "\r" ])
    | "\\" [ "n", "t", "r", "\\", "\'" ]
    )*
     ["\n","\r"] >
	{
	  System.err.println("Line " + input_stream.getEndLine() + " - Char constant has a : " + image);
	  countLexError++;
	}
|
	< INVALID_STRING_CONST:
	"\""
    (
      (~[ "\"", "\\", "\n", "\r" ])
    | "\\" [ "n", "t", "r", "\\", "\"" ]
    )*
     ["\n","\r"] >
	{
	  System.err.println("Line " + input_stream.getEndLine() + " - String constant has a : " + image);
	  countLexError++;
	}

}


ListNode paramlist(RecoverySet g) throws ParseEOFException :
{
  ListNode 	p = null,
  			q = null;
  int k = 0;
  Token t1 = null,
  		t2 = null;
}
{
  try {
    [
    	( t1 =  < INT > | t1 =  < STRING > | t1 =  < CHAR > | t1 =  < BOOLEAN > | t1 =  < FLOAT > | t1 =  < IDENT >)
    	  t2 = < IDENT > (< LBRACKET > < RBRACKET > { k++; })*
    	{
		  q = new ListNode(new VarNode(t2, k));
     	  p = new ListNode(new VarDeclNode(t1, q));
    	}
    	( < COMMA > { k = 0; }
    	(t1 = < INT > | t1 = < STRING > | t1 = < CHAR > | t1 = < BOOLEAN > | t1 = < FLOAT > | t1 = < IDENT >)
    	t2 = < IDENT > ( < LBRACKET > <RBRACKET > { k++; } )*
    	{
		  q = new ListNode(new VarNode(t2, k));
     	  p = new ListNode(new VarDeclNode(t1, q));
    	}
   		)*
   	] { return p; }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "paramlist");
    return null;
  }
}

/* paramlist antigo:
 [
		(< INT > | < STRING > | < CHAR > | < BOOLEAN > | < FLOAT > | < IDENT >) < IDENT >
		(< LBRACKET > < RBRACKET >)* [< ASSIGN > (alocexpression() | expression() )]
		(
		  < COMMA > (
			(< INT > | < STRING > | < CHAR > | < BOOLEAN > | < FLOAT > | < IDENT >) < IDENT >
			(< LBRACKET > < RBRACKET >)* [< ASSIGN > (alocexpression() | expression() )]
		  )
		)*
  ]

  */

VarDeclNode vardecl(RecoverySet g) throws ParseEOFException :
{
  Token t1 = null,
  		t2;
  int k = 0;
  ListNode l = null;
}
{
  try {
	(t1 = < INT > | t1 =  < STRING > | t1 =  < CHAR > | t1 =  < BOOLEAN > | t1 =  < FLOAT > | t1 =  < IDENT >)
	t2 = < IDENT > (< LBRACKET > < RBRACKET > { k++; })*
	{ l = new ListNode(new VarNode(t2, k)); }
	(< COMMA > { k = 0; } t2 = < IDENT > (< LBRACKET > < RBRACKET > { k++; })*
	{ l.add(new VarNode(t2, k)); }
	)*
// TODO Adaptar expressão com árvore sitática para atribuição de valor
// 	[< ASSIGN > (alocexpression(g) | expression(g) )]
	{ return new VarDeclNode(t1, l); }
  }
  catch (ParseException e) {
    consumeUntil(g, e, "vardecl");
    return new VarDeclNode(t1, l);
  }
}

ExpreNode factor() throws ParseEOFException :
{
  ExpreNode e = null;
  Token t;
}
{
  (
    t = < int_constant > { e = new IntConstNode(t); }
    |
    t = < string_constant > { e = new StringConstNode(t); }
    |
    t = < char_constant > { e = new CharConstNode(t); }
    |
    t = < null_constant > { e = new NullConstNode(t); }
    |
    t = < boolean_constant > { e = new BooleanConstNode(t); }
    |
    t = < float_constant > { e = new FloatConstNode(t); }
    |
    e = lvalue(null)
    |
    < LPAREN > e = expression(null) < RPAREN >
  )
  { return e; }
}

ExpreNode unaryexpr() throws ParseEOFException :
{
  ExpreNode e;
  Token t = null, tn = null;
}
{
  [ tn = < NOT >][(t = < PLUS > | t = < MINUS >)] e = factor()
  { return ( (t == null) ? e : new UnaryNode(t, tn, e) );}
}

ExpreNode term() throws ParseEOFException :
{
  ExpreNode e1 = null,
  			e2;
  Token t;
}
{
  e1 = unaryexpr()
  (
    ( t = < MULTIPLY > | t = < DIVIDE > | t = < REM >)
      e2 = unaryexpr()
      { e1 = new MultNode(t, e1, e2); }
  )*
  { return e1; } 
}

ExpreNode numexpr() throws ParseEOFException :
{
  ExpreNode e1 = null, e2;
  Token t;
}
{
  e1 = term()
  (
    ( t = < PLUS > | t = < MINUS >)
    e2 = term()
    { e1 = new AddNode(t, e1, e2); }
  )*
    { return e1; }
}


ExpreNode equaltyExpression() :
{
  ExpreNode e1 = null, e2;
  Token t;
}
{
  e1 = numexpr()
  (
    ( t = < LT > | t =  < GT > | t =  < LE > | t =  < GE > | t =  < EQ > | t =  < NEQ > )
      e2 = numexpr()
      { e1 = new EqualtyNode(t, e1, e2); }
    )*
    { return e1; }
}

ExpreNode condANDexpression():
{
  ExpreNode e1 = null, e2;
  Token t;
}
{
  e1 = equaltyExpression()
  (
    ( t = < AND >)
    e2 = equaltyExpression()
    { e1 = new AndNode(t, e1, e2); }
  )*
  { return e1; }
}

ExpreNode condORexpression():
{
  ExpreNode e1 = null, e2;
  Token t;
}
{
  e1 = condANDexpression()
  (
    (t = < OR > | t = < XOR >)
    e2 = condANDexpression()
    { e1 = new AndNode(t, e1, e2); }
  )*
  { return e1; }
}


ExpreNode expression(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null, e2 = null;
  Token t = null;
}
{
  try {
    e1 = numexpr()
    [
      (t = < LT > | t =  < GT > | t =  < LE > | t =  < GE > | t =  < EQ > | t =  < NEQ >)
      e2 = numexpr()
	  { e1 = new RelationalNode(t, e1, e2); }
    ]
    { return e1; }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "expression");
    return new RelationalNode(t, e1, e2);
  }
}


ExpreNode alocexpression(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null, e2 = null;
  ListNode l = null;
  Token t1, t2;
  
  RecoverySet f1 = new RecoverySet(RPAREN);
  RecoverySet f2 = new RecoverySet(RBRACKET);
}
{
  t1 = < NEW >
  (
	LOOKAHEAD(2) t2 = < IDENT >  < LPAREN > l = arglist(f1) < RPAREN >
	{ e1 = new NewObjectNode(t1, t2, l); }
	|
	( t2 = < INT > | t2 =  < STRING > | t2 =  < CHAR > | t2 =  < BOOLEAN > | t2 =  < FLOAT > | t2 =  < IDENT >)
	(< LBRACKET > e2 = expression(f2) < RBRACKET >
	  {
        if ( l == null )
           l = new ListNode(e2);
        else
           l.add(e2);
      }
	)+
	{ e1 = new NewArrayNode(t1, t2, l); }
  )
  { return e1; }
}

ExpreNode lvalue(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null,
            e2 = null;
  Token t1 = null,
        t2 = null;
  ListNode l = null;
}
{
  try { 
  t1 = < IDENT > { e1 = new VarNode(t1); }
  (
    t1 = < LBRACKET > e2 = expression(null) < RBRACKET >
    { e1 = new IndexNode(t1, e1, e2); }
  |
//  	LOOKAHEAD(3) Isto está no livro, não sei se se aplica ao nosso caso
    t1 = < DOT > t2 = <IDENT > [< LPAREN > l = arglist(null) < RPAREN >]
    { e1 = new CallNode(t1, e1, t2, l); }
  )*
  { return e1; }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "lvalue");
    return new VarNode(t1);
  }
}


AtribNode atribstat(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null, e2 = null;
  Token t1 = null;
  
  RecoverySet f1 = new RecoverySet(ASSIGN);
}
{
  try {
    e1 = lvalue(f1) t1 = < ASSIGN >
    (e2 = alocexpression(g) | e2 = expression(g))
    { return new AtribNode(t1, e1, e2); }
  }
  catch (ParseException e)
  {
	consumeUntil(g, e, "atribstat");
	return new AtribNode(t1, e1, e2);
  }
}

PrintNode printstat(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null;
  Token t = null;
}
{
  try { 
  	t = < PRINT > e1 = expression(g)
  	{ return new PrintNode(t, e1); }
 }
  catch (ParseException e)
  {
	consumeUntil(g, e, "printstat");
	return new PrintNode(t, e1);
  }
}

ReadNode readstat(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null;
  Token t = null;
}
{
  try {
    t = < READ > e1 = lvalue(g)
    { return new ReadNode(t, e1); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "readstat");
    return new ReadNode(t, e1);
  }
}

ReturnNode returnstat(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null;
  Token t = null;
}
{
  try {
    t = < RETURN > [e1 = expression(g)]
    { return new ReturnNode(t, e1); }
  }
  catch(ParseException e)
  {
    consumeUntil(g, e, "returnstat");
    return new ReturnNode(t, e1);
  }
    
}

SuperNode superstat(RecoverySet g) throws ParseEOFException :
{
  ListNode l = null;
  Token t = null;
  
  RecoverySet f = new RecoverySet(RPAREN);
}
{
  try { 
  	t = < SUPER > < LPAREN > l = arglist(f) < RPAREN >
  	{ return new SuperNode(t, l); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "superstat");
    return new SuperNode(t, l);
  }
}


StatementNode statement(RecoverySet g) throws ParseEOFException :
{
StatementNode s = null;
ListNode l;
Token t1 = null;

RecoverySet f1 = new RecoverySet(SEMICOLON).union(g).remove(IDENT);
RecoverySet f2 = new RecoverySet(RBRACE).union(g).remove(IDENT);
}
{
try {

(
    LOOKAHEAD(<IDENT> <IDENT>)
    s = vardecl(f1) <SEMICOLON>
|
    LOOKAHEAD(1)
    s = atribstat(f1) <SEMICOLON>
|
    s = vardecl(f1) <SEMICOLON>
|
    s = printstat(f1) <SEMICOLON>
|
    s = readstat(f1) <SEMICOLON>
|
    s = returnstat(f1) <SEMICOLON>
|
    s = superstat(f1) <SEMICOLON>
|
    s = ifstat(g)
|
    s = forstat(g)
|
    t1 = <LBRACE> l = statlist(f2) <RBRACE> { s = new BlockNode(t1, l); }
|
    t1 = <BREAK> <SEMICOLON> { s = new BreakNode(t1);}
|
    t1 = <SEMICOLON> { s = new NopNode(t1); }
)  {return s;}
}
catch (ParseException e)
{
   consumeUntil(g, e, "statement");
   return new NopNode(t1);
}
}

MethodBodyNode methodbody(RecoverySet g) throws ParseEOFException :
{
  Token t1 = null;
  ListNode l = null;
  StatementNode s = null;
  
  RecoverySet f = new RecoverySet(RPAREN).union(g);
}
{
  try {
  	t1 = < LPAREN > l = paramlist(f) < RPAREN > s = statement(g)
  	{ return new MethodBodyNode(t1, l, s); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "methodbody");
    return new MethodBodyNode(t1, l, s);
  }
}


MethodDeclNode methoddecl(RecoverySet g) throws ParseEOFException:
{
  Token t1 = null, t2 = null;
  int k = 0;
  MethodBodyNode m = null;
}
{
  try {
	( t1 = < INT > | t1 =  < STRING > | t1 =  < CHAR > | t1 =  < BOOLEAN > | t1 =  < FLOAT > | t1 =  < IDENT >)
	( < LBRACKET > < RBRACKET > { k++; } )*
	t2 = < IDENT > m = methodbody(g)
	{ return new MethodDeclNode(t1, k, t2, m); }
  }
  catch (ParseEOFException e) {
	consumeUntil(g, e, "methoddecl");
	return new MethodDeclNode(t1, k, t2, m);
  }
}

ConstructDeclNode constructdecl(RecoverySet g) throws ParseEOFException:
{
  Token t = null;
  MethodBodyNode m = null;
}
{
  try {
    t = < CONSTRUCTOR > m = methodbody(g)
    { return new ConstructDeclNode(t, m); } 
  }
  catch (ParseEOFException e) {
	consumeUntil(g, e, "constructdecl");
	return new ConstructDeclNode(t, m);
  }
}

void Type():
{
}
{
 (
   < INT > | < BOOLEAN > | < CHAR > | < STRING > | < FLOAT > | < IDENT >
 )
}


void MethodDeclarationLookahead() :
{}
{
  Type() <IDENT> "("
}

ClassBodyNode classbody(RecoverySet g) throws ParseEOFException :
{
  ListNode c = null, v = null, ct = null, m = null;
  VarDeclNode vd;
  ConstructDeclNode cd;
  MethodDeclNode md;
  Token t = null;
  
  RecoverySet f1 = new RecoverySet(RBRACE),
  f2 = new RecoverySet(SEMICOLON),
  f3 = First.methoddecl.union(f1),
  f4 = First.constructdecl.union(f3),
  f5 = First.vardecl.union(f4);
}
{
  try { 
	t = < LBRACE >
		[c = classlist(f5)]
		(LOOKAHEAD(3) vd = vardecl(f2) < SEMICOLON >
		{ if (v == null) v = new ListNode(vd);
		  else v.add(v);
		}
		)*
		(cd = constructdecl(f4)
		{ if (ct == null) ct = new ListNode(cd);
		  else ct.add(cd);
		}
		)*
		(md = methoddecl(f3)
		{ if (m == null) m = new ListNode(md);
		  else m.add(md);
		}
		)*
	< RBRACE >
	{ return new ClassBodyNode (t, c, v, ct, m); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "classbody");
    return new ClassBodyNode (t, c, v, ct, m);
  }
}

ClassDeclNode classdecl(RecoverySet g) throws ParseEOFException :
{
  Token t = null, n = null, e1 = null;
  ClassBodyNode c = null;
}
{
  try {
     t = <CLASS>  n = <IDENT> [ <EXTENDS> e1 = <IDENT> ] c = classbody(g)
     { return new ClassDeclNode(t, n, e1, c); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "classdecl");
    return new ClassDeclNode(t, n, e1, c);
  }
}


IfNode ifstat(RecoverySet g) throws ParseEOFException :
{
  ExpreNode e1 = null;
  StatementNode s1 = null,
                s2 = null;
  Token t = null;

  RecoverySet f1 = new RecoverySet(RPAREN),
  f2 = new RecoverySet(ELSE).union(g);
}
{
  try { 
    t = < IF > < LPAREN > e1 = expression(f1) < RPAREN > s1 = statement(f2)
    [LOOKAHEAD(1) < ELSE > s2 = statement(g)]
    { return new IfNode(t, e1, s1, s2); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "ifstat");
    return new IfNode(t, e1, s1, s2);
  }
}

ForNode forstat(RecoverySet g) throws ParseEOFException :
{
  AtribNode s1 = null,
            s2 = null;
  StatementNode s3 = null;
  ExpreNode e1 = null;
  Token t = null;

  RecoverySet f1 = new RecoverySet(SEMICOLON),
  f2 = new RecoverySet(RPAREN);
}
{
  try { 
    t = < FOR > < LPAREN > [s1 = atribstat(f1)] < SEMICOLON >
  					 	   [e1 = expression(f1)] < SEMICOLON >
  					       [s2 = atribstat(f2)] < RPAREN >
  						    s3 = statement(g)
   { return new ForNode(t, e1, s1, s2, s3); }
  }
  catch (ParseException e)
  {
    consumeUntil(g, e, "forstat");
    return new ForNode(t, e1, s1, s2, s3);
  }
}

ListNode  statlist(RecoverySet g) throws ParseEOFException :
{
ListNode l = null;
StatementNode s = null;

RecoverySet f = First.statlist.remove(IDENT).union(g);
}
{
   s = statement(f) [ l = statlist(g)]
    { return new ListNode(s, l); }
}


ListNode classlist(RecoverySet g) throws ParseEOFException :
{
  ClassDeclNode c = null;
  ListNode l = null;
  
  RecoverySet f = First.classlist.union(g);
}
{
  (
  	c = classdecl(f) [ l = classlist(g) ]
  	{ return new ListNode(c, l);}
  )
}


ListNode arglist(RecoverySet g) throws ParseEOFException :
{
  ListNode l = null;
  ExpreNode e = null;

  RecoverySet f = new RecoverySet(COMMA).union(g);
}
{
  [
    e = expression(f)
      { l = new ListNode(e); }
    (< COMMA > e = expression(f)
      { l.add(e); }
    )*
  ]
  { return l;}
}

ListNode program() throws ParseEOFException :
{
//RecoverySet g = new RecoverySet(EOF);
RecoverySet g = First.program;

ListNode l = null, d = null;
}
{
  < EOF >
  | 
  ( l = classlist(g)
  try {
	< EOF > { return l; }
  }
  catch (ParseException e) {
	consumeUntil(g, e, "program");
  }
  [ d = program() ]
  ) { return l; } 
}